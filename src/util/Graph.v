From coqutil Require Import Map.Interface.
From coqutil Require Import Map.Properties.
From coqutil Require Import Map.MapKeys.
From coqutil Require Import Semantics.OmniSmallstepCombinators.
From coqutil Require Import Eqb.
From Stdlib Require Import List PeanoNat Permutation Lia.
From Stdlib Require Import RelationClasses Morphisms Classical_Prop.
From Datalog Require Import OmniSmallstep Smallstep Map List Eqb.
From Datalog Require Import Tactics.
From coqutil Require Import Tactics Tactics.fwd.
Import ListNotations.

Abbreviation node_id := nat (only parsing).

Variant source :=
  | node_source (_ : node_id)
  | input_source.

#[export] Instance source_eqb : Eqb source :=
  fun s1 s2 =>
    match s1, s2 with
    | node_source n1, node_source n2 => eqb n1 n2
    | input_source, input_source => true
    | _, _ => false
    end.

#[export] Instance source_eqb_ok : Eqb_ok source_eqb.
Proof.
  intros a b. destruct a, b; cbn; try congruence.
  destr (eqb n n0); congruence.
Qed.

Variant destn :=
  | node_destn (_ : node_id)
  | output_destn.

#[export] Instance destn_eqb : Eqb destn :=
  fun d1 d2 =>
    match d1, d2 with
    | node_destn n1, node_destn n2 => eqb n1 n2
    | output_destn, output_destn => true
    | _, _ => false
    end.

#[export] Instance destn_eqb_ok : Eqb_ok destn_eqb.
Proof.
  intros a b. destruct a, b; cbn; try congruence.
  destr (eqb n n0); congruence.
Qed.

Variant location :=
  | node_loc (_ : node_id)
  | input_loc
  | output_loc.

#[export] Instance location_eqb : Eqb location :=
  fun l1 l2 =>
    match l1, l2 with
    | node_loc n1, node_loc n2 => eqb n1 n2
    | input_loc, input_loc => true
    | output_loc, output_loc => true
    | _, _ => false
    end.

#[export] Instance location_eqb_ok : Eqb_ok location_eqb.
Proof.
  intros a b. destruct a, b; cbn; try congruence.
  destr (eqb n n0); congruence.
Qed.

Section __.
  Context {message : Type}.
  Context {label : Type}.
  Context (forward : source -> destn -> message -> bool).

  Context (equiv : message -> message -> Prop).
  Context {equiv_equiv : Equivalence equiv}.
  Context (forward_equiv :
            forall s d a b, equiv a b -> forward s d a = forward s d b).
  Context {stmt : Type}.
  Context (claim : stmt -> list message -> Prop).
  Context (claim_output : stmt -> source -> list message -> Prop).
  Context (claim_mono :
            forall s ms1 ms2, claim s ms1 ->
                         incl_mod equiv ms1 ms2 ->
                         claim s ms2).

  Context (consistent_output : stmt -> source -> list message -> Prop).
  Context (claim_output_mono :
            forall s n ms1 ms2, claim_output s n ms1 ->
                           incl_mod equiv ms1 ms2 ->
                           claim_output s n ms2).
  Context (allowed_output : source -> list message -> Prop).
  Context (consistent : stmt -> list message -> Prop).
  Context {node_map : map.map source (list message)}.
  Context {node_map_ok : map.ok node_map}.

  Context (consistent_mono :
            forall s ms1 ms2,
            consistent s ms1 ->
            submultiset ms1 ms2 ->
            consistent s ms2).

  Context (consistent_output_mono :
            forall s n ms1 ms2,
            consistent_output s n ms1 ->
            submultiset ms1 ms2 ->
            consistent_output s n ms2).

  Local Abbreviation IO_event := (Smallstep.IO_event label message).

  Variant graph_label :=
    | receive (_ : node_id) (_ : message)
    | run (_ : node_id) (_ : label)
    | emit (_ : message).

  Local Abbreviation gevent := (Smallstep.IO_event graph_label message).

  (*we could alo consider something like this?*)
  (*
    Definition claim_good :=
    forall s nodes mss,
      is_all_nodes nodes ->
      Forall2 allowed_output nodes mss ->
      (claim s (concat mss) <->
         Forall2 (claim_output s) nodes mss).

Definition consistent_good :=
    forall s nodes mss,
      is_all_nodes nodes ->
      Forall2 allowed_output nodes mss ->
      (consistent s (concat mss) <->
         Forall2 (consistent_output s) nodes mss).
   *)

  Definition consistent_good :=
    forall s (partition : node_map),
      Forall_map allowed_output partition ->
      claim s (concat (values partition)) ->
      Forall_map (claim_output s) partition /\
        (consistent s (concat (values partition)) <->
           Forall_map (consistent_output s) partition).

  Context (consistent_good_holds : consistent_good).

  Context (allowed : list message -> Prop).
  Context (allowed_submultiset : multiset_monotone_dec allowed).
  Context (allowed_output_submultiset : forall n, multiset_monotone_dec (allowed_output n)).
  Context (allowed_of_outputs :
             forall (partition : node_map),
               Forall_map allowed_output partition -> allowed (concat (values partition))).

  Lemma Permutation_allowed l1 l2 : Permutation l1 l2 -> allowed l2 -> allowed l1.
  Proof.
    intros HP Ha. eapply allowed_submultiset; [ exact Ha | ].
    exists []. rewrite app_nil_r. symmetry. exact HP.
  Qed.

  Definition noncontradictory := noncontradictory_wf equiv claim consistent allowed.

  Definition noncontradictory_output (k : source) :=
    noncontradictory_wf equiv (fun s => claim_output s k) (fun s => consistent_output s k)
                        (allowed_output k).

  Lemma noncontradictory_witness_map (p1 p2 : node_map) :
    Forall2_map noncontradictory_output p1 p2 ->
    exists pM : node_map,
      Forall2_map (fun _ v m => submultiset v m) p1 pM /\
      Forall_map allowed_output pM /\
      Forall2_map (fun k v m => consistently_incl equiv (fun s => claim_output s k)
                                                 (fun s => consistent_output s k) v m) p2 pM /\
      Forall2_map noncontradictory_output p2 pM.
  Proof.
    intros H.
    assert (Hex : Forall2_map (fun k v1 v2 => exists m,
        submultiset v1 m /\ allowed_output k m /\
        consistently_incl equiv (fun s => claim_output s k) (fun s => consistent_output s k) v2 m /\
        noncontradictory_output k v2 m) p1 p2).
    { eapply Forall2_map_impl; [ eassumption | ].
      cbv [noncontradictory_output]. invert 1. eauto. }
    destruct (Forall2_map_choose _ _ _ Hex) as (pM & HM). exists pM. repeat split.
    - eapply Forall3_map_forget_m; eapply Forall3_map_impl; [ exact HM | ].
      intros k v1 v2 m Hq. exact (proj1 Hq).
    - intros k m Hget. destruct (Forall3_map_get_r _ _ _ _ _ _ HM Hget) as (v1 & v2 & _ & _ & Hq).
      exact (proj1 (proj2 Hq)).
    - eapply Forall3_map_forget_l. eapply Forall3_map_impl; [ eassumption | ].
      simpl. intros. fwd. eassumption.
    - eapply Forall3_map_forget_l. eapply Forall3_map_impl; [ eassumption | ].
      simpl. intros. fwd. eassumption.
  Qed.

  (*TODO using this lemma should simplify some proofs later in the file?*)
  Lemma consistently_incl_concat_values (p2 pM : node_map) :
    Forall_map allowed_output p2 ->
    Forall_map allowed_output pM ->
    Forall2_map (fun k v m => consistently_incl equiv (fun s => claim_output s k)
                                                (fun s => consistent_output s k) v m) p2 pM ->
    consistently_incl equiv claim consistent (concat (values p2)) (concat (values pM)).
  Proof.
    intros Hallow2 HallowM H.
    assert (Hincl : incl_mod equiv (concat (values p2)) (concat (values pM))).
    { apply incl_mod_concat_values; try exact equiv_equiv.
      eapply Forall2_map_impl; [ exact H | ].
      intros k v m Hci. exact (proj1 Hci). }
    unfold consistently_incl. split; [ exact Hincl | ].
    cbv [consistent_le]. intros s Hcl Hcon.
    assert (Hclaim_pM : claim s (concat (values pM)))
      by (eapply claim_mono; [ exact Hcl | exact Hincl ]).
    pose proof (consistent_good_holds s pM HallowM Hclaim_pM) as (_ & HbicondM).
    pose proof (consistent_good_holds s p2 Hallow2 Hcl) as (Hclaim_out2 & Hbicond2).
    apply (proj1 Hbicond2) in Hcon.
    apply (proj2 HbicondM). intros k m Hget.
    destruct (Forall2_map_get_r _ _ _ _ _ H Hget) as (v & Hv & Hci).
    destruct Hci as (_ & Hcle). apply Hcle.
    - exact (Hclaim_out2 k v Hv).
    - exact (Hcon k v Hv).
  Qed.

  Lemma noncontradictory_of_outputs (partition1 partition2 : node_map) :
    Forall_map allowed_output partition2 ->
    Forall2_map noncontradictory_output partition1 partition2 ->
    noncontradictory (concat (values partition1)) (concat (values partition2)).
  Proof.
    revert partition1 partition2.
    cofix CIH. intros p1 p2 Hallow2 H.
    destruct (noncontradictory_witness_map p1 p2 H) as (pM & Hsub & HallowM & Hci & Htail).
    unfold noncontradictory. econstructor.
    - apply submultiset_concat_values. exact Hsub.
    - apply allowed_of_outputs. exact HallowM.
    - apply consistently_incl_concat_values; [ exact Hallow2 | exact HallowM | exact Hci ].
    - apply CIH; [ exact HallowM | exact Htail ].
  Qed.

  Definition matching_inps n (inps : list message) :=
    filter (forward input_source (node_destn n)) inps.

  Definition graph_inputs_allowed (inps : list message) :=
    forall n, allowed_output input_source (matching_inps n inps).

  Definition noncontradictory_graph_inputs (inps1 inps2 : list message) :=
    forall n, noncontradictory_output input_source (matching_inps n inps1) (matching_inps n inps2).

  Section graph.
    Context {node_state : Type} (node_step : node_id -> node_state -> IO_event -> node_state -> Prop).

    Record graph_node_state :=
      { gns_node_state : node_state;
        gns_trace : list IO_event;
        gns_queue : list message }.

    Context {m1 : map.map node_id graph_node_state} {m1_ok : map.ok m1}.

    Record graph_state :=
      { graph_nodes : partial_map node_id graph_node_state;
        graph_output_queue : list message }.

    Definition enqueue inps gns :=
      {| gns_node_state := gns.(gns_node_state);
        gns_trace := gns.(gns_trace);
        gns_queue := inps ++ gns.(gns_queue) |}.

    Definition forward_to (keep : destn -> message -> bool) (msgs : list message) (gs : graph_state) : graph_state :=
      {| graph_nodes :=
           map_values' (fun dst => enqueue (filter (keep (node_destn dst)) msgs)) gs.(graph_nodes);
         graph_output_queue :=
           filter (keep output_destn) msgs ++ gs.(graph_output_queue) |}.

    Variant receive_step (n : node_id) : graph_node_state -> message -> graph_node_state -> Prop :=
    | receive_step_intro ns ns' m ms1 ms2 :
      node_step n ns.(gns_node_state) (I_event m) ns' ->
      ns.(gns_queue) = ms1 ++ m :: ms2 ->
      receive_step n ns m
        {| gns_node_state := ns'; gns_trace := I_event m :: ns.(gns_trace); gns_queue := ms1 ++ ms2 |}.

    Inductive graph_step : graph_state -> gevent -> graph_state -> Prop :=
    | gstep_input gs m :
      graph_step gs (I_event m) (forward_to (forward input_source) [m] gs)
    | gstep_run gs n ns ns' lbl outs :
      map.get gs.(graph_nodes) n = Some ns ->
      node_step n ns.(gns_node_state) (O_event lbl outs) ns' ->
      graph_step gs (O_event (run n lbl) [])
        (forward_to (forward (node_source n)) outs
           {| graph_nodes :=
                map.put gs.(graph_nodes) n
                        {| gns_node_state := ns';
                          gns_trace := O_event lbl outs :: ns.(gns_trace);
                          gns_queue := ns.(gns_queue) |};
              graph_output_queue := gs.(graph_output_queue) |})
    | gstep_receive gs n ns m ns' :
      map.get gs.(graph_nodes) n = Some ns ->
      receive_step n ns m ns' ->
      graph_step gs (O_event (receive n m) [])
        {| graph_output_queue := gs.(graph_output_queue);
           graph_nodes := map.put gs.(graph_nodes) n ns' |}
    | gstep_output gs q1 m q2 :
      gs.(graph_output_queue) = q1 ++ m :: q2 ->
      graph_step gs (O_event (emit m) [m])
        {| graph_nodes := gs.(graph_nodes);
          graph_output_queue := q1 ++ q2 |}.

    Lemma star_receive_step nodes oq n ns ms ns' :
      map.get nodes n = Some ns ->
      star (receive_step n) ns ms ns' ->
      star graph_step {| graph_nodes := nodes; graph_output_queue := oq |}
        (map (fun m => O_event (receive n m) []) ms)
        {| graph_nodes := map.put nodes n ns'; graph_output_queue := oq |}.
    Proof.
      intros Hget Hstar. revert nodes Hget. induction Hstar; intros nodes Hget.
      - rewrite map.put_noop by assumption. apply star_refl.
      - simpl. eapply star_step; [apply IHHstar; assumption |].
        rewrite <- (map.put_put_same n s' s''). eapply gstep_receive; [apply map.get_put_same | assumption].
    Qed.

    Lemma star_per_node_gen (b : graph_state) todo :
      forall a,
        a.(graph_output_queue) = b.(graph_output_queue) ->
        (forall n, ~ In n todo -> map.get a.(graph_nodes) n = map.get b.(graph_nodes) n) ->
        (forall n, In n todo ->
           exists na nb ms, map.get a.(graph_nodes) n = Some na /\ map.get b.(graph_nodes) n = Some nb /\
             star (receive_step n) na ms nb) ->
        exists t, star graph_step a t b.
    Proof.
      induction todo as [| k todo IH]; intros a Hoq Hout Hin.
      - exists []. replace a with b; [apply star_refl|].
        destruct a, b. simpl in *. f_equal; [| congruence].
        apply map.map_ext. intros k. specialize (Hout k ltac:(intros [])). congruence.
      - destruct a as [nodes oq]. simpl in *.
        destruct (Hin k (in_eq _ _)) as (na & nb & ms & Ha & Hb & Hstar).
        edestruct (IH {| graph_nodes := map.put nodes k nb; graph_output_queue := oq |}) as (t & Hrest); simpl.
        + assumption.
        + intros n Hn. destr (eqb n k).
          * rewrite map.get_put_same. congruence.
          * rewrite map.get_put_diff by congruence. apply Hout. intros [-> | ?]; auto.
        + intros n Hn. destr (eqb n k).
          * exists nb, nb, []. rewrite map.get_put_same. auto using star_refl.
          * rewrite map.get_put_diff by congruence. apply Hin. right. assumption.
        + eexists. eapply star_app; [eapply star_receive_step; eassumption | exact Hrest].
    Qed.

    Lemma star_per_node a b :
      a.(graph_output_queue) = b.(graph_output_queue) ->
      Forall2_map (fun n na nb => exists ms, star (receive_step n) na ms nb) a.(graph_nodes) b.(graph_nodes) ->
      exists t, star graph_step a t b.
    Proof.
      intros Hoq HF. apply (star_per_node_gen b (map.keys a.(graph_nodes))); [assumption | |].
      - intros n Hn. specialize (HF n). destruct (map.get a.(graph_nodes) n) eqn:E.
        + exfalso. apply Hn. eapply map.in_keys. exact E.
        + destruct (map.get b.(graph_nodes) n); [contradiction | reflexivity].
      - intros n Hn. apply map.in_keys_inv in Hn. specialize (HF n).
        destruct (map.get a.(graph_nodes) n) as [na |]; [| contradiction].
        destruct (map.get b.(graph_nodes) n) as [nb |]; [| contradiction]. fwd. eauto 6.
    Qed.

    Ltac invert_receive :=
      match goal with H : receive_step _ _ _ _ |- _ => invert H end.

    Context {msg_map : map.map node_id (list message)}.
    Context {msg_map_ok : map.ok msg_map}.
    Context (initial_gs : graph_state).
    Context (initial_gs_empty :
               forall n gns, map.get initial_gs.(graph_nodes) n = Some gns ->
                             gns.(gns_trace) = [] /\ gns.(gns_queue) = []).
    Context (initial_output_queue_empty : initial_gs.(graph_output_queue) = []).
    Context (nodes_input_total : forall n, input_total (node_step n)).

    Definition good_inputs_from n inps :=
        allowed_output n inps /\
          forall c, claim_output c n inps -> consistent_output c n inps.

    Definition good_node_output n outs :=
      forall dest, good_inputs_from (node_source n) (filter (forward (node_source n) (node_destn dest)) outs).

    Definition node_good (n : node_id) : graph_node_state -> Prop :=
      fun gns =>
        outputs_well_formed    (node_step n) (good_node_output n) gns.(gns_node_state) /\
        monotone_mod_equiv     (node_step n) equiv claim consistent allowed gns.(gns_node_state) /\
        might_implies_will_equiv (node_step n) equiv allowed gns.(gns_node_state).

    Definition outputs_partition (gs : partial_map node_id (graph_node_state)) : msg_map :=
      map_values' (fun _ ns => flat_map outputs_of ns.(gns_trace)) gs.

    Definition output_map {A} {mp' : map.map node_id (list A)} {mp'_ok : map.ok mp'}
        (F : node_id -> list message -> list A) (gs : partial_map node_id (graph_node_state)) : list A :=
      concat (values (map_values' (mp' := mp') F (outputs_partition gs))).

    Definition fwd_partition (nn : node_id) (gs : partial_map node_id (graph_node_state)) : msg_map :=
      map_values' (fun sender outs => filter (forward (node_source sender) (node_destn nn)) outs) (outputs_partition gs).

    Definition fwd_total (nn : node_id) (gs : partial_map node_id (graph_node_state)) : list message :=
      output_map (fun sender outs => filter (forward (node_source sender) (node_destn nn)) outs) gs.

    Lemma fwd_total_eq nn gs : fwd_total nn gs = concat (values (fwd_partition nn gs)).
    Proof. reflexivity. Qed.

    Definition le_weak (g1 g2 : graph_state) :=
      Forall2_map (fun _ => incl_mod equiv) (outputs_partition g1.(graph_nodes)) (outputs_partition g2.(graph_nodes)).

    Definition le (g1 g2 : graph_state) :=
      Forall2_map (fun n gns1 gns2 =>
                     consistently_incl equiv claim consistent (flat_map inputs_of gns1.(gns_trace)) (flat_map inputs_of gns2.(gns_trace)))
        g1.(graph_nodes) g2.(graph_nodes).

    Definition node_has_output (gs : graph_state) (n : node_id) (o : message) : Prop :=
      exists ns, map.get gs.(graph_nodes) n = Some ns /\ In o (flat_map outputs_of (gns_trace ns)).

    Let graph_will_step := (will_step graph_step graph_inputs_allowed).

    Context (nodes_good : Forall_map node_good initial_gs.(graph_nodes)).

    #[local] Hint Constructors star eventually : core.
    #[local] Hint Resolve
      incl_mod_refl incl_mod_of_incl incl_mod_trans
      in_or_app impl_in_map impl_in_filter star_app submultiset_app_r : core.
    #[local] Hint Unfold val_sat might_output : core.
    #[local] Hint Extern 5 (In _ _) => simpl : core.

    Lemma le_weak_refl g : le_weak g g.
    Proof. apply Forall2_map_refl. auto using incl_mod_refl. Qed.

    Lemma le_refl g : le g g.
    Proof. apply Forall2_map_refl. auto using consistently_incl_refl. Qed.

    Lemma le_weak_trans g1 g2 g3 :
      le_weak g1 g2 -> le_weak g2 g3 -> le_weak g1 g3.
    Proof. apply Forall2_map_trans. eauto using incl_mod_trans. Qed.

    Lemma graph_step_to_node_step gs gt gs' :
      star graph_step gs gt gs' ->
      Forall2_map (fun n gns1 gns2 =>
                     exists t'',
                       gns2.(gns_trace) = t'' ++ gns1.(gns_trace) /\
                         star (node_step n) gns1.(gns_node_state) t'' gns2.(gns_node_state))
        gs.(graph_nodes) gs'.(graph_nodes).
    Proof.
      induction 1 as [ | gt2 smid e gs' Hstar IH Hstep].
      - apply Forall2_map_dup. intros n gns _. exists []. ssplit; eauto.
      - invert Hstep; try invert_receive; cbn [forward_to graph_nodes graph_output_queue].
        + apply Forall2_map_map_values'_r. eapply Forall2_map_impl; [ exact IH | ].
          intros k v1 v2 Hr. cbn [enqueue gns_trace gns_node_state]. exact Hr.
        + apply Forall2_map_map_values'_r. simpl.
          epose proof (Forall2_map_get_r _ _ _ _ _ IH H) as (v1 & Hv1 & Hrel). fwd.
          eapply Forall2_map_put_r; try eassumption.
          -- eapply Forall2_map_impl; [eassumption|]. simpl. intros. fwd. eauto.
          -- simpl. rewrite Hrelp0. eexists (_ :: _). simpl. eauto.
        + simpl. epose proof (Forall2_map_get_r _ _ _ _ _ IH H) as (v1 & Hv1 & Hrel).
          eapply Forall2_map_put_r; try eassumption.
          -- eapply Forall2_map_impl; [eassumption|]. simpl. intros. fwd. eauto.
          -- simpl. fwd. rewrite Hrelp0. eexists (_ :: _). simpl. eauto.
        + exact IH.
    Qed.

    Lemma graph_step_to_node_step_from_beginning gs gt :
      star graph_step initial_gs gt gs ->
      Forall2_map (fun n gns0 gns =>
                     star (node_step n) gns0.(gns_node_state) gns.(gns_trace) gns.(gns_node_state))
        initial_gs.(graph_nodes) gs.(graph_nodes).
    Proof.
      intros. eapply Forall2_map_impl_strong.
      { eapply graph_step_to_node_step; eauto. }
      intros n gns0 gns H1 _ (t'' & Htr & Hst).
      apply initial_gs_empty in H1. destruct H1 as [Htr0 _].
      rewrite Htr0, app_nil_r in Htr. subst t''. eauto.
    Qed.

    (* the visible outputs of every node, each tagged with its node. *)
    Definition output_total (gs : graph_state) : list message :=
      output_map (mp' := msg_map)
        (fun k outs => filter (forward (node_source k) output_destn) outs) gs.(graph_nodes).

    Lemma outputs_partition_get gs n :
      map.get (outputs_partition gs) n =
        option_map (fun ns => flat_map outputs_of ns.(gns_trace)) (map.get gs n).
    Proof. apply get_map_values'. Qed.

    Lemma fwd_partition_get nn gs sender :
      map.get (fwd_partition nn gs) sender =
        option_map (fun ns => filter (forward (node_source sender) (node_destn nn)) (flat_map outputs_of ns.(gns_trace))) (map.get gs sender).
    Proof.
      unfold fwd_partition. rewrite get_map_values', outputs_partition_get.
      destruct (map.get gs sender); reflexivity.
    Qed.

    Lemma outputs_partition_put gs n v :
      outputs_partition (map.put gs n v) = map.put (outputs_partition gs) n (flat_map outputs_of v.(gns_trace)).
    Proof.
      apply map.map_ext. intro j.
      rewrite outputs_partition_get, !map.get_put_dec, outputs_partition_get.
      destruct (Nat.eqb n j); reflexivity.
    Qed.

    Lemma outputs_partition_map_values' (g : node_id -> graph_node_state -> graph_node_state) M :
      (forall k v, (g k v).(gns_trace) = v.(gns_trace)) ->
      outputs_partition (map_values' g M) = outputs_partition M.
    Proof.
      intros Hg. apply map.map_ext. intro j.
      rewrite !outputs_partition_get, get_map_values'.
      destruct (map.get M j) as [w|]; cbn [option_map]; [ rewrite Hg | ]; reflexivity.
    Qed.

    Lemma outputs_partition_put_output_eq gs n v0 v :
      map.get gs n = Some v0 ->
      flat_map outputs_of v.(gns_trace) = flat_map outputs_of v0.(gns_trace) ->
      outputs_partition (map.put gs n v) = outputs_partition gs.
    Proof.
      intros Hn Ho.
      assert (Hg : map.get (outputs_partition gs) n = Some (flat_map outputs_of v0.(gns_trace)))
        by (rewrite outputs_partition_get, Hn; reflexivity).
      rewrite outputs_partition_put, Ho.
      rewrite (map.put_noop n (flat_map outputs_of v0.(gns_trace)) (outputs_partition gs) Hg). reflexivity.
    Qed.

    Lemma outputs_partition_mupd_enqueue gs n inps :
      outputs_partition (mupd gs n (enqueue inps)) = outputs_partition gs.
    Proof.
      unfold mupd. destruct (map.get gs n) as [v|] eqn:Hn; [ | reflexivity ].
      apply (outputs_partition_put_output_eq gs n v _ Hn). reflexivity.
    Qed.

    Lemma output_map_run {A} {mp' : map.map node_id (list A)} {mp'_ok : map.ok mp'}
        (F : node_id -> list message -> list A)
        (Hdist : forall k a b, F k (a ++ b) = F k a ++ F k b)
        gs n ns lbl outs ns' :
      map.get gs n = Some ns ->
      Permutation
        (output_map (mp' := mp') F
           (map_values' (fun k => enqueue (filter (forward (node_source n) (node_destn k)) outs))
              (map.put gs n {| gns_node_state := ns';
                               gns_trace := O_event lbl outs :: ns.(gns_trace);
                               gns_queue := ns.(gns_queue) |})))
        (F n outs ++ output_map F gs).
    Proof.
      intros Hn. unfold output_map.
      erewrite outputs_partition_map_values'; [|reflexivity].
      rewrite outputs_partition_put. eapply Permutation_trans; [ apply concat_values_map_values'_put | ].
      simpl. rewrite Hdist, <- app_assoc. apply Permutation_app_head.
      apply Permutation_sym.
      apply concat_values_map_values'_get.
      rewrite outputs_partition_get, Hn. reflexivity.
    Qed.

    Lemma output_map_mupd_enqueue {A} {mp' : map.map node_id (list A)} {mp'_ok : map.ok mp'}
        (F : node_id -> list message -> list A) gs n inps :
      output_map (mp' := mp') F (mupd gs n (enqueue inps)) = output_map F gs.
    Proof. unfold output_map. rewrite outputs_partition_mupd_enqueue. reflexivity. Qed.

    Lemma output_map_map_values'_trace {A} {mp' : map.map node_id (list A)} {mp'_ok : map.ok mp'}
        (F : node_id -> list message -> list A) g gs :
      (forall k v, (g k v).(gns_trace) = v.(gns_trace)) ->
      output_map (mp' := mp') F (map_values' g gs) = output_map F gs.
    Proof. intros Hg. unfold output_map. rewrite outputs_partition_map_values' by exact Hg. reflexivity. Qed.

    Lemma output_map_put_output_eq {A} {mp' : map.map node_id (list A)} {mp'_ok : map.ok mp'}
        (F : node_id -> list message -> list A) gs n v0 v :
      map.get gs n = Some v0 ->
      flat_map outputs_of v.(gns_trace) = flat_map outputs_of v0.(gns_trace) ->
      output_map (mp' := mp') F (map.put gs n v) = output_map F gs.
    Proof.
      intros Hn Ho. unfold output_map.
      erewrite outputs_partition_put_output_eq; eauto.
    Qed.

    Lemma output_map_initial {A} {mp' : map.map node_id (list A)} {mp'_ok : map.ok mp'}
        (F : node_id -> list message -> list A) :
      (forall k, F k [] = []) -> output_map (mp' := mp') F initial_gs.(graph_nodes) = [].
    Proof.
      intros Hnil. unfold output_map. apply concat_nil_Forall, values_Forall.
      intros k v Hv. rewrite get_map_values', outputs_partition_get in Hv.
      apply option_map_Some in Hv. fwd. apply option_map_Some in Hvp0. fwd.
      pose proof initial_gs_empty as He. especialize He; eauto.
      fwd. erewrite Hep0. auto.
    Qed.

    Lemma outputs_are_node_outputs gt gs :
      star graph_step initial_gs gt gs ->
      Permutation (flat_map outputs_of gt ++ gs.(graph_output_queue))
                  (output_total gs ++ filter (forward input_source output_destn) (flat_map inputs_of gt)).
    Proof.
      unfold output_total.
      induction 1 as [ | gt0 gmid e gs Hstar IH Hstep ].
      - cbn [flat_map]. rewrite initial_output_queue_empty.
        rewrite output_map_initial by (intros; reflexivity). reflexivity.
      - invert Hstep; try invert_receive; cbn [forward_to graph_nodes graph_output_queue outputs_of inputs_of flat_map].
        + rewrite app_nil_l, output_map_map_values'_trace by reflexivity.
          rewrite (filter_app _ [m]).
          eapply perm_trans; [ apply Permutation_app_swap_app | ].
          eapply perm_trans; [ | apply Permutation_app_swap_app ].
          apply Permutation_app_head. exact IH.
        + rewrite !app_nil_l.
          rewrite (output_map_run (fun k outs => filter (forward (node_source k) output_destn) outs)
                     ltac:(intros; apply filter_app) _ _ _ _ _ _ H).
          rewrite <- app_assoc.
          eapply perm_trans; [ apply Permutation_app_swap_app | ].
          apply Permutation_app_head. exact IH.
        + rewrite app_nil_l, app_nil_l.
          erewrite (output_map_put_output_eq _ _ _ ns) by (eassumption || reflexivity). exact IH.
        + rewrite H in IH. eapply perm_trans; [ | exact IH ]. cbn [app].
          rewrite !app_assoc. apply Permutation_middle.
    Qed.

    Lemma matching_inps_app nn (e1 e2 : list message) :
      matching_inps nn (e1 ++ e2) = matching_inps nn e1 ++ matching_inps nn e2.
    Proof. unfold matching_inps. apply filter_app. Qed.

    Lemma matching_inps_single nn m :
      matching_inps nn [m] = if forward input_source (node_destn nn) m then [m] else [].
    Proof. unfold matching_inps. cbn [filter]. destruct (forward input_source (node_destn nn) m); reflexivity. Qed.

    Lemma matching_inps_perm nn e1 e2 :
      Permutation e1 e2 -> Permutation (matching_inps nn e1) (matching_inps nn e2).
    Proof. intros HP. unfold matching_inps. rewrite HP. reflexivity. Qed.

    Hint Resolve matching_inps_perm : core.

    Lemma star_gstep_le_weak g T g' : star graph_step g T g' -> le_weak g g'.
    Proof.
      intros Hstar. cbv [le_weak outputs_partition].
      apply Forall2_map_map_values'_l. apply Forall2_map_map_values'_r.
      eapply Forall2_map_impl; [ apply (graph_step_to_node_step g T g' Hstar) | ].
      intros k gns1 gns2 (t'' & Htr & _). rewrite Htr, flat_map_app.
      apply (incl_mod_of_incl equiv). intros x Hx. apply in_or_app. right. exact Hx.
    Qed.

    Lemma incl_mod_filter_forward sender nn l1 l2 :
      incl_mod equiv l1 l2 ->
      incl_mod equiv (filter (forward (node_source sender) (node_destn nn)) l1) (filter (forward (node_source sender) (node_destn nn)) l2).
    Proof.
      intros Hincl x Hx. apply filter_In in Hx. destruct Hx as (Hin & Hfwd).
      destruct (Hincl x Hin) as (x' & Hin' & Hequiv).
      exists x'. split; [ apply filter_In; split; [ exact Hin' | ] | exact Hequiv ].
      rewrite <- (forward_equiv (node_source sender) (node_destn nn) x x' Hequiv). exact Hfwd.
    Qed.

    Lemma le_weak_fwd_partition g1 g2 n :
      le_weak g1 g2 ->
      Forall2_map (fun _ => incl_mod equiv) (fwd_partition n g1.(graph_nodes)) (fwd_partition n g2.(graph_nodes)).
    Proof.
      intros Hle. unfold fwd_partition.
      apply Forall2_map_map_values'_l, Forall2_map_map_values'_r.
      eapply Forall2_map_impl; [ exact Hle | ].
      intros k v1 v2 Hincl. apply incl_mod_filter_forward. exact Hincl.
    Qed.

    Definition conserved (gs : graph_state) (ext : list message) : Prop :=
      forall nn nsn, map.get gs.(graph_nodes) nn = Some nsn ->
        Permutation (flat_map inputs_of nsn.(gns_trace) ++ nsn.(gns_queue))
                    (fwd_total nn gs.(graph_nodes) ++ matching_inps nn ext).

    Lemma fwd_total_map_values'_trace nn g gs :
      (forall k v, (g k v).(gns_trace) = v.(gns_trace)) ->
      fwd_total nn (map_values' g gs) = fwd_total nn gs.
    Proof. intros Hg. unfold fwd_total. apply output_map_map_values'_trace. exact Hg. Qed.

    Lemma conservation_step gs e gs' :
      graph_step gs e gs' ->
      forall ext, conserved gs ext -> conserved gs' (ext ++ inputs_of e).
    Proof.
      intros Hstep ext IH. cbv [conserved] in IH |- *. intros nn nsn Hg'.
      invert Hstep; try invert_receive; cbn [forward_to graph_nodes graph_output_queue] in Hg' |- *.
      - rewrite get_map_values' in Hg'. apply option_map_Some in Hg'. fwd.
        cbn [inputs_of]. rewrite matching_inps_app.
        rewrite fwd_total_map_values'_trace by reflexivity.
        cbn [enqueue gns_trace gns_queue].
        change (filter (forward input_source (node_destn nn)) [m]) with (matching_inps nn [m]).
        rewrite (app_assoc (fwd_total nn gs.(graph_nodes))).
        eapply perm_trans; [ | apply Permutation_app_tail; exact (IH nn _ Hg'p0) ].
        rewrite <- app_assoc. apply Permutation_app_head. apply Permutation_app_comm.
      - rewrite get_map_values', map.get_put_dec in Hg'.
        apply option_map_Some in Hg'. fwd.
        simpl. rewrite app_nil_r.
        etransitivity;
          [ | symmetry; apply Permutation_app_tail;
              apply (output_map_run (fun sender outs => filter (forward (node_source sender) (node_destn nn)) outs)
                       ltac:(intros; apply filter_app) gs.(graph_nodes) n ns lbl outs ns' H) ].
        destr_sth Nat.eqb.
        + fwd. cbn [enqueue gns_trace gns_queue]. simpl.
          rewrite <- app_assoc. eauto with perm.
        + cbn [enqueue gns_trace gns_queue].
          rewrite <- app_assoc. eauto with perm.
      - rewrite map.get_put_dec in Hg'. simpl. rewrite app_nil_r.
        etransitivity;
          [ | symmetry; apply Permutation_app_tail;
              unfold fwd_total;
              erewrite output_map_put_output_eq by (eassumption || reflexivity); reflexivity ].
        destr_sth Nat.eqb; eauto.
        fwd. simpl. specialize (IH _ _ ltac:(eassumption)). rewrite H2 in IH.
        eauto with perm.
      - cbn [inputs_of]. rewrite app_nil_r. exact (IH nn nsn Hg').
    Qed.

    Lemma conserved_perm_ext gs e1 e2 :
      Permutation e1 e2 -> conserved gs e1 -> conserved gs e2.
    Proof.
      intros HP Hc nn nsn Hget.
      eapply perm_trans; [ apply (Hc nn nsn Hget) | eauto with perm ].
    Qed.

    Lemma conservation_gen gs0 T gs1 :
      star graph_step gs0 T gs1 ->
      forall ext, conserved gs0 ext -> conserved gs1 (ext ++ flat_map inputs_of T).
    Proof.
      induction 1 as [ | T0 smid e sfin Hstar IH Hstep ]; intros ext Hconv.
      - cbn [inputs_of flat_map]. rewrite app_nil_r. exact Hconv.
      - eapply conserved_perm_ext;
          [ | apply (conservation_step _ _ _ Hstep _ (IH ext Hconv)) ].
        simpl. repeat rewrite <- app_assoc. eauto with perm.
    Qed.

    Lemma inputs_are_outputs gt gs :
      star graph_step initial_gs gt gs ->
      Forall_map (fun nn ns =>
                    Permutation (flat_map inputs_of ns.(gns_trace) ++ ns.(gns_queue))
                                (fwd_total nn gs.(graph_nodes) ++ matching_inps nn (flat_map inputs_of gt)))
        gs.(graph_nodes).
    Proof.
      intros Hstar. cbv [Forall_map]. intros nn nsn Hget.
      assert (Hbase : conserved initial_gs []).
      { intros k v Hget0. pose proof (initial_gs_empty k v Hget0) as [Ht Hq].
        rewrite Ht, Hq. unfold fwd_total.
        rewrite output_map_initial by (intros; reflexivity). reflexivity. }
      pose proof (conservation_gen initial_gs gt gs Hstar [] Hbase) as Hcons.
      rewrite app_nil_l in Hcons. exact (Hcons nn nsn Hget).
    Qed.

    Lemma fwd_partition_good gt gs nn :
      star graph_step initial_gs gt gs ->
      Forall_map (fun sender => good_inputs_from (node_source sender)) (fwd_partition nn gs.(graph_nodes)).
    Proof.
      intros Hstar sender v Hv. rewrite fwd_partition_get in Hv.
      destruct (map.get gs.(graph_nodes) sender) as [gns|] eqn:Hgs; cbn [option_map] in Hv; [ | discriminate ].
      injection Hv as Hv. subst v.
      pose proof (graph_step_to_node_step_from_beginning gs gt Hstar) as Hnodes.
      destruct (Forall2_map_get_r _ _ _ _ _ Hnodes Hgs) as (gns0 & Hget0 & Hrun).
      pose proof (nodes_good sender gns0 Hget0) as (Howf & _ & _).
      exact (Howf _ _ Hrun nn).
    Qed.

    Definition with_external (partition : msg_map) (ext : list message) : node_map :=
      map.put (map.map_keys node_source partition) input_source ext.

    Lemma with_external_get_input partition ext :
      map.get (with_external partition ext) input_source = Some ext.
    Proof. unfold with_external. apply map.get_put_same. Qed.

    Lemma with_external_get_node partition ext k :
      map.get (with_external partition ext) (node_source k) = map.get partition k.
    Proof.
      unfold with_external. rewrite map.get_put_diff by discriminate.
      apply map.get_map_keys_always_invertible. congruence.
    Qed.

    Lemma values_with_external partition ext :
      Permutation (concat (values (with_external partition ext)))
                  (ext ++ concat (values partition)).
    Proof.
      unfold with_external.
      eapply Permutation_trans.
      { apply Permutation_concat, values_put_fresh.
        apply get_map_keys_not_in_image. intros ?; discriminate. }
      cbn [concat]. apply Permutation_app_head, Permutation_concat.
      apply values_map_keys. intros a b H; congruence.
    Qed.

    Lemma Forall_map_with_external (P : source -> list message -> Prop) partition ext :
      P input_source ext ->
      Forall_map (fun n => P (node_source n)) partition ->
      Forall_map P (with_external partition ext).
    Proof.
      intros HN HS q v Hq. destruct q as [k|].
      - rewrite with_external_get_node in Hq. exact (HS k v Hq).
      - rewrite with_external_get_input in Hq. injection Hq as Hq. subst v. exact HN.
    Qed.

    Lemma Forall2_map_with_external (R : source -> list message -> list message -> Prop)
      part1 part2 ext1 ext2 :
      R input_source ext1 ext2 ->
      Forall2_map (fun k => R (node_source k)) part1 part2 ->
      Forall2_map R (with_external part1 ext1) (with_external part2 ext2).
    Proof.
      intros HN HS q. destruct q as [k|].
      - rewrite !with_external_get_node. exact (HS k).
      - rewrite !with_external_get_input. exact HN.
    Qed.

    Lemma everything_allowed gt gs :
      star graph_step initial_gs gt gs ->
      graph_inputs_allowed (flat_map inputs_of gt) ->
      Forall_map (fun _ ns => allowed (flat_map inputs_of ns.(gns_trace) ++ ns.(gns_queue))) gs.(graph_nodes).
    Proof.
      intros Hstar Hallow. cbv [Forall_map]. intros nn nsn Hget.
      eapply Permutation_allowed.
      - apply (inputs_are_outputs gt gs Hstar nn nsn Hget).
      - rewrite fwd_total_eq.
        eapply Permutation_allowed with
          (l2 := concat (values (with_external (fwd_partition nn gs.(graph_nodes))
                                               (matching_inps nn (flat_map inputs_of gt))))).
        + eapply Permutation_trans; [ apply Permutation_app_comm | ].
          apply Permutation_sym, values_with_external.
        + apply allowed_of_outputs. apply Forall_map_with_external; [ apply Hallow | ].
          intros k v Hk. exact (proj1 (fwd_partition_good gt gs nn Hstar k v Hk)).
    Qed.

    Lemma noncontradictory_output_filter n dest l1 l2 :
      noncontradictory_wf equiv
        (fun s l => claim_output s (node_source n) (filter (forward (node_source n) (node_destn dest)) l))
        (fun s l => consistent_output s (node_source n) (filter (forward (node_source n) (node_destn dest)) l))
        (fun l => good_inputs_from (node_source n) (filter (forward (node_source n) (node_destn dest)) l))
        l1 l2 ->
      noncontradictory_output (node_source n)
        (filter (forward (node_source n) (node_destn dest)) l1) (filter (forward (node_source n) (node_destn dest)) l2).
    Proof.
      revert l1 l2. cofix CIH. intros l1 l2 Hnc.
      destruct Hnc as [l1' Hsub Hwf [Hincl Hle] Htail].
      unfold noncontradictory_output. econstructor.
      - apply submultiset_filter. exact Hsub.
      - exact (proj1 Hwf).
      - split.
        + apply incl_mod_filter_forward. exact Hincl.
        + intros s Hcl Hcon. exact (Hle s Hcl Hcon).
      - apply CIH. exact Htail.
    Qed.

    Lemma node_output_noncontradictory n dest init tr1 s1 tr2 s2 :
      outputs_well_formed (node_step n) (good_node_output n) init ->
      monotone_mod_equiv (node_step n) equiv claim consistent allowed init ->
      might_implies_will_equiv (node_step n) equiv allowed init ->
      star (node_step n) init tr1 s1 ->
      star (node_step n) init tr2 s2 ->
      allowed (flat_map inputs_of tr2) ->
      noncontradictory (flat_map inputs_of tr1) (flat_map inputs_of tr2) ->
      noncontradictory_output (node_source n)
        (filter (forward (node_source n) (node_destn dest)) (flat_map outputs_of tr1))
        (filter (forward (node_source n) (node_destn dest)) (flat_map outputs_of tr2)).
    Proof.
      intros Howf Hmono Hmiw Hstar1 Hstar2 Hallow2 Hnc.
      apply noncontradictory_output_filter.
      eapply noncontradictory_outputs_of_inputs with
        (claim := claim) (consistent := consistent) (allowed := allowed)
        (claim_output := fun s l => claim_output s (node_source n) (filter (forward (node_source n) (node_destn dest)) l))
        (consistent_output := fun s l => consistent_output s (node_source n) (filter (forward (node_source n) (node_destn dest)) l))
        (outputs_wf := fun l => good_inputs_from (node_source n) (filter (forward (node_source n) (node_destn dest)) l))
        (initial := init).
      - exact equiv_equiv.
      - exact allowed_submultiset.
      - exact consistent_mono.
      - exact claim_mono.
      - intros s l1 l2 Hcl Hincl. eapply claim_output_mono;
          [ exact Hcl | apply incl_mod_filter_forward; exact Hincl ].
      - intros s l Hgi Hcl. exact (proj2 Hgi s Hcl).
      - apply miw'_iff_miw_and_monotone'; try assumption;
          split; [ exact Hmiw | exact Hmono ].
      - exact (nodes_input_total n).
      - intros t s Hstar. exact (Howf t s Hstar dest).
      - exact Hstar1.
      - exact Hstar2.
      - exact Hallow2.
      - exact Hnc.
    Qed.

    Lemma noncontradictory_output_refl k v :
      allowed_output k v -> noncontradictory_output k v v.
    Proof.
      revert v. cofix CIH. intros v Hallow. unfold noncontradictory_output. econstructor.
      - apply submultiset_refl.
      - exact Hallow.
      - split; [ exact (incl_mod_refl equiv v) | intros s _ Hc; exact Hc ].
      - apply CIH. exact Hallow.
    Qed.

    Lemma everything_noncontradictory_refl gt gs dest :
      star graph_step initial_gs gt gs ->
      Forall2_map (fun n => noncontradictory_output (node_source n))
        (fwd_partition dest gs.(graph_nodes)) (fwd_partition dest gs.(graph_nodes)).
    Proof.
      intros Hstar k. destruct (map.get (fwd_partition dest gs.(graph_nodes)) k) as [v|] eqn:Hk; [ | exact I ].
      apply noncontradictory_output_refl.
      exact (proj1 (fwd_partition_good gt gs dest Hstar k v Hk)).
    Qed.

    Lemma noncontradictory_shrink l1 l1' l2 l2' :
      submultiset l1' l1 -> submultiset l2' l2 ->
      noncontradictory l1 l2 -> noncontradictory l1' l2'.
    Proof.
      intros Hs1 Hs2 H.
      eapply noncontradictory_wf_shrink_l with (l1 := l1); try assumption.
      eapply noncontradictory_wf_shrink_r with (l2 := l2); try assumption.
    Qed.

    Lemma claim_output_mono_k k : forall s, incl_mod_mono_inc equiv (claim_output s k).
    Proof. intros s l1 l2 Hc Hincl. eapply claim_output_mono; eauto. Qed.

    Lemma consistent_output_mono_k k : forall s, multiset_monotone_inc (consistent_output s k).
    Proof. intros s l1 l2 Hc Hsub. eapply consistent_output_mono; eauto. Qed.

    Lemma noncontradictory_output_sym k a b :
      noncontradictory_output k a b -> noncontradictory_output k b a.
    Proof.
      pose proof (claim_output_mono_k k) as Hcm.
      pose proof (consistent_output_mono_k k) as Hsm.
      intros [a' Hsa Hwfa Hcia [b' Hsb Hwfb Hcib Htail]].
      unfold noncontradictory_output. econstructor.
      - exact Hsb.
      - exact Hwfb.
      - eapply consistently_incl_shrink_l with (l1 := a'); try assumption.
      - eapply noncontradictory_wf_shrink_l with (l1 := a'); try assumption.
    Qed.

    Lemma node_inputs_noncontradictory n t1 gs1 t2 gs2 gns1 gns2 :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      map.get gs1.(graph_nodes) n = Some gns1 ->
      map.get gs2.(graph_nodes) n = Some gns2 ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      Forall2_map (fun sender => noncontradictory_output (node_source sender))
        (fwd_partition n gs1.(graph_nodes)) (fwd_partition n gs2.(graph_nodes)) ->
      noncontradictory (flat_map inputs_of gns1.(gns_trace)) (flat_map inputs_of gns2.(gns_trace)).
    Proof.
      intros Hstar1 Hstar2 Hg1 Hg2 Hga2 Hnci Hfwd.
      assert (Hallow2 : Forall_map allowed_output
        (with_external (fwd_partition n gs2.(graph_nodes)) (matching_inps n (flat_map inputs_of t2)))).
      { apply Forall_map_with_external;
          [ exact (Hga2 n)
          | intros k v Hk; exact (proj1 (fwd_partition_good t2 gs2 n Hstar2 k v Hk)) ]. }
      assert (Hnc12 : Forall2_map noncontradictory_output
        (with_external (fwd_partition n gs1.(graph_nodes)) (matching_inps n (flat_map inputs_of t1)))
        (with_external (fwd_partition n gs2.(graph_nodes)) (matching_inps n (flat_map inputs_of t2)))).
      { apply Forall2_map_with_external; [ exact (Hnci n) | exact Hfwd ]. }
      pose proof (noncontradictory_of_outputs _ _ Hallow2 Hnc12) as Hnco.
      assert (Hsub1 : submultiset (flat_map inputs_of gns1.(gns_trace))
        (concat (values (with_external (fwd_partition n gs1.(graph_nodes)) (matching_inps n (flat_map inputs_of t1)))))).
      { exists (gns1.(gns_queue)).
        eapply Permutation_trans; [ apply values_with_external | ].
        rewrite <- fwd_total_eq.
        eapply Permutation_trans; [ apply Permutation_app_comm | ].
        symmetry. exact (inputs_are_outputs t1 gs1 Hstar1 n gns1 Hg1). }
      assert (Hsub2 : submultiset (flat_map inputs_of gns2.(gns_trace))
        (concat (values (with_external (fwd_partition n gs2.(graph_nodes)) (matching_inps n (flat_map inputs_of t2)))))).
      { exists (gns2.(gns_queue)).
        eapply Permutation_trans; [ apply values_with_external | ].
        rewrite <- fwd_total_eq.
        eapply Permutation_trans; [ apply Permutation_app_comm | ].
        symmetry. exact (inputs_are_outputs t2 gs2 Hstar2 n gns2 Hg2). }
      apply (noncontradictory_shrink _ _ _ _ Hsub1 Hsub2 Hnco).
    Qed.

    Lemma everything_nc_preserve_r t1 gs1 t2 gs2 e gs2' dest :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      graph_step gs2 e gs2' ->
      graph_inputs_allowed (flat_map inputs_of (e :: t2)) ->
      noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of (e :: t2)) ->
      (forall d, Forall2_map (fun n => noncontradictory_output (node_source n))
         (fwd_partition d gs1.(graph_nodes)) (fwd_partition d gs2.(graph_nodes))) ->
      Forall2_map (fun n => noncontradictory_output (node_source n))
        (fwd_partition dest gs1.(graph_nodes)) (fwd_partition dest gs2'.(graph_nodes)).
    Proof.
      intros Hstar1 Hstar2 Hstep Hga Hnci Hfwd.
      invert Hstep; try invert_receive; cbn [forward_to graph_nodes graph_output_queue].
      - match goal with |- Forall2_map _ _ (fwd_partition _ ?g) =>
          assert (Heq : fwd_partition dest g = fwd_partition dest gs2.(graph_nodes)) end.
        { unfold fwd_partition. rewrite outputs_partition_map_values' by reflexivity.
          reflexivity. }
        rewrite Heq. exact (Hfwd dest).
      - assert (Heq : fwd_partition dest
          (map_values' (fun m => enqueue (filter (forward (node_source n) (node_destn m)) outs))
             (map.put gs2.(graph_nodes) n {| gns_node_state := ns'; gns_trace := O_event lbl outs :: gns_trace ns;
                               gns_queue := gns_queue ns |}))
          = map.put (fwd_partition dest gs2.(graph_nodes)) n
              (filter (forward (node_source n) (node_destn dest)) (flat_map outputs_of (O_event lbl outs :: gns_trace ns)))).
        { unfold fwd_partition.
          rewrite (outputs_partition_map_values'
                     (fun m => enqueue (filter (forward (node_source n) (node_destn m)) outs)) _ ltac:(intros; reflexivity)).
          rewrite outputs_partition_put, map_values'_put. reflexivity. }
        rewrite Heq. clear Heq.
        pose proof (Hfwd dest n) as Hfn.
        rewrite !fwd_partition_get, H in Hfn. cbn [option_map] in Hfn.
        destruct (map.get gs1.(graph_nodes) n) as [gns1|] eqn:Hg1; [ | contradiction ].
        pose proof (graph_step_to_node_step_from_beginning gs1 t1 Hstar1) as Hns1.
        pose proof (graph_step_to_node_step_from_beginning gs2 t2 Hstar2) as Hns2.
        destruct (Forall2_map_get_r _ _ _ _ _ Hns1 Hg1) as (gns0 & Hget0 & Hrun1).
        destruct (Forall2_map_get_r _ _ _ _ _ Hns2 H) as (gns0' & Hget0' & Hrun2).
        rewrite Hget0 in Hget0'. injection Hget0' as Hget0'. subst gns0'.
        pose proof (nodes_good n gns0 Hget0) as (Howf & Hmono & Hmiw).
        eapply Forall2_map_put_r.
        + eapply Forall2_map_impl; [ exact (Hfwd dest) | intros k w1 w2 Hw _; exact Hw ].
        + rewrite fwd_partition_get, Hg1. reflexivity.
        + eapply node_output_noncontradictory.
          * exact Howf.
          * exact Hmono.
          * exact Hmiw.
          * exact Hrun1.
          * eapply star_step; [ exact Hrun2 | exact H0 ].
          * cbn [flat_map inputs_of app].
            eapply allowed_submultiset;
              [ exact (everything_allowed t2 gs2 Hstar2 Hga n ns H) | apply submultiset_app_r ].
          * cbn [flat_map inputs_of app].
            eapply node_inputs_noncontradictory;
              [ exact Hstar1 | exact Hstar2 | exact Hg1 | exact H
              | exact Hga | exact Hnci | exact (Hfwd n) ].
      - match goal with |- Forall2_map _ _ (fwd_partition _ ?g) =>
          assert (Heq : fwd_partition dest g = fwd_partition dest gs2.(graph_nodes)) end.
        { unfold fwd_partition.
          erewrite outputs_partition_put_output_eq; [ reflexivity | eassumption | reflexivity ]. }
        rewrite Heq. exact (Hfwd dest).
      - exact (Hfwd dest).
    Qed.

    Lemma submultiset_matching_inps nn l1 l2 :
      submultiset l1 l2 -> submultiset (matching_inps nn l1) (matching_inps nn l2).
    Proof.
      intros (rest & Hperm). exists (matching_inps nn rest).
      rewrite <- matching_inps_app. apply matching_inps_perm. exact Hperm.
    Qed.

    Lemma graph_inputs_allowed_submultiset i1 i2 :
      submultiset i1 i2 -> graph_inputs_allowed i2 -> graph_inputs_allowed i1.
    Proof.
      intros Hsub Hga n.
      eapply allowed_output_submultiset; [ apply (Hga n) | ].
      apply submultiset_matching_inps. exact Hsub.
    Qed.

    Lemma noncontradictory_output_shrink_r k a b b' :
      submultiset b' b -> noncontradictory_output k a b -> noncontradictory_output k a b'.
    Proof.
      intros Hsub H.
      pose proof (claim_output_mono_k k) as Hcm.
      pose proof (consistent_output_mono_k k) as Hsm.
      eapply noncontradictory_wf_shrink_r with (l2 := b); try assumption.
    Qed.

    Lemma noncontradictory_graph_inputs_sym i1 i2 :
      noncontradictory_graph_inputs i1 i2 -> noncontradictory_graph_inputs i2 i1.
    Proof. intros H n. apply noncontradictory_output_sym. exact (H n). Qed.

    Lemma noncontradictory_graph_inputs_submultiset i1 i2 i2' :
      submultiset i2' i2 ->
      noncontradictory_graph_inputs i1 i2 -> noncontradictory_graph_inputs i1 i2'.
    Proof.
      intros Hsub H n. eapply noncontradictory_output_shrink_r;
        [ apply submultiset_matching_inps; exact Hsub | exact (H n) ].
    Qed.

    Lemma noncontradictory_graph_inputs_of_submultiset i1 i2 :
      submultiset i1 i2 -> graph_inputs_allowed i2 -> noncontradictory_graph_inputs i1 i2.
    Proof.
      intros Hsub Hga n. eapply noncontradictory_wf_of_submultiset; try exact equiv_equiv.
      - apply submultiset_matching_inps. exact Hsub.
      - exact (Hga n).
    Qed.

    Lemma submultiset_inputs_tl {L M} (e : Smallstep.IO_event L M) (t : list (Smallstep.IO_event L M)) :
      submultiset (flat_map inputs_of t) (flat_map inputs_of (e :: t)).
    Proof. cbn [flat_map]. apply submultiset_app_l. Qed.

    Lemma everything_noncontradictory_sym gs1 gs2 dest :
      Forall2_map (fun n => noncontradictory_output (node_source n)) (fwd_partition dest gs1.(graph_nodes)) (fwd_partition dest gs2.(graph_nodes)) ->
      Forall2_map (fun n => noncontradictory_output (node_source n)) (fwd_partition dest gs2.(graph_nodes)) (fwd_partition dest gs1.(graph_nodes)).
    Proof.
      intros H k. specialize (H k).
      destruct (map.get (fwd_partition dest gs1.(graph_nodes)) k) as [a|];
        destruct (map.get (fwd_partition dest gs2.(graph_nodes)) k) as [b|]; try exact H.
      apply noncontradictory_output_sym. exact H.
    Qed.

    Lemma everything_noncontradictory t1 gs1 t2 gs2 dest :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      graph_inputs_allowed (flat_map inputs_of t1) ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      Forall2_map (fun n => noncontradictory_output (node_source n))
        (fwd_partition dest gs1.(graph_nodes)) (fwd_partition dest gs2.(graph_nodes)).
    Proof.
      revert t1 gs1 t2 gs2 dest.
      enough (H : forall N t1 gs1 t2 gs2 dest,
        length t1 + length t2 <= N ->
        star graph_step initial_gs t1 gs1 -> star graph_step initial_gs t2 gs2 ->
        graph_inputs_allowed (flat_map inputs_of t1) -> graph_inputs_allowed (flat_map inputs_of t2) ->
        noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of t2) ->
        Forall2_map (fun n => noncontradictory_output (node_source n))
          (fwd_partition dest gs1.(graph_nodes)) (fwd_partition dest gs2.(graph_nodes))).
      { intros t1 gs1 t2 gs2 dest.
        apply (H (length t1 + length t2)). apply Nat.le_refl. }
      induction N as [ | N IHN ];
        intros t1 gs1 t2 gs2 dest Hlen Hstar1 Hstar2 Hga1 Hga2 Hnci.
      - assert (t1 = [] /\ t2 = []) as [Ht1 Ht2]
          by (destruct t1, t2; simpl in Hlen; (lia || auto)).
        subst t1 t2. apply star_nil in Hstar1. apply star_nil in Hstar2. subst gs1 gs2.
        eapply everything_noncontradictory_refl. constructor.
      - destruct t2 as [ | e2 t2' ].
        + apply star_nil in Hstar2. subst gs2. destruct t1 as [ | e1 t1' ].
          * apply star_nil in Hstar1. subst gs1.
            eapply everything_noncontradictory_refl. constructor.
          * invert Hstar1.
            apply everything_noncontradictory_sym.
            eapply everything_nc_preserve_r.
            -- constructor.
            -- eassumption.
            -- eassumption.
            -- exact Hga1.
            -- apply noncontradictory_graph_inputs_sym. exact Hnci.
            -- intro d. eapply IHN with (t1 := []) (t2 := t1').
               ++ simpl in Hlen |- *. lia.
               ++ constructor.
               ++ eassumption.
               ++ exact Hga2.
               ++ eapply graph_inputs_allowed_submultiset;
                    [ apply submultiset_inputs_tl | exact Hga1 ].
               ++ eapply noncontradictory_graph_inputs_submultiset;
                    [ apply submultiset_inputs_tl
                    | apply noncontradictory_graph_inputs_sym; exact Hnci ].
        + invert Hstar2.
          eapply everything_nc_preserve_r.
          -- exact Hstar1.
          -- eassumption.
          -- eassumption.
          -- exact Hga2.
          -- exact Hnci.
          -- intro d. eapply IHN with (t1 := t1) (t2 := t2').
             ++ simpl in Hlen |- *. lia.
             ++ exact Hstar1.
             ++ eassumption.
             ++ exact Hga1.
             ++ eapply graph_inputs_allowed_submultiset;
                  [ apply submultiset_inputs_tl | exact Hga2 ].
             ++ eapply noncontradictory_graph_inputs_submultiset;
                  [ apply submultiset_inputs_tl | exact Hnci ].
    Qed.

    Lemma node_run_allowed t gs :
      star graph_step initial_gs t gs ->
      graph_inputs_allowed (flat_map inputs_of t) ->
      Forall2_map (fun n gns0 gns =>
                     star (node_step n) (gns_node_state gns0) (gns_trace gns) (gns_node_state gns) /\
                     allowed (flat_map inputs_of (gns_trace gns)))
        initial_gs.(graph_nodes) gs.(graph_nodes).
    Proof.
      intros Hstar Hallow.
      pose proof (everything_allowed t gs Hstar Hallow) as Hall.
      eapply Forall2_map_impl_strong;
        [ eapply graph_step_to_node_step_from_beginning; exact Hstar | ].
      intros n gns0 gns Hget0 Hget Hrun. split; [ exact Hrun | ].
      eapply allowed_submultiset; [ apply (Hall n gns Hget) | apply submultiset_app_r ].
    Qed.

    Lemma graph_will_step_of_node_will_step n P gs gt gns :
      star graph_step initial_gs gt gs ->
      map.get gs.(graph_nodes) n = Some gns ->
      will_step (node_step n) allowed (gns.(gns_node_state), gns.(gns_trace)) P ->
      graph_will_step
        (gs, gt)
        (fun '(gs', _) =>
           val_sat gs'.(graph_nodes) n (fun gns' => P (gns'.(gns_node_state), gns'.(gns_trace)))).
    Proof.
      intros H Hn Hns. induction Hns.
      - cbv [graph_will_step will_step]. eexists. intros.
        pose proof H1 as HD.
        apply graph_step_to_node_step in H1.
        eapply Forall2_map_get_l in H1; eauto. fwd.
        specialize (H0 _ _ ltac:(eassumption)). specialize' H0.
        { eapply allowed_submultiset.
          - eapply everything_allowed. 2: eassumption. all: eauto.
          - rewrite H1p1p0. eexists. apply Permutation_refl. }
        destruct H0; fwd.
        + left. cbv [val_sat]. eexists. rewrite H1p1p0. eauto.
        + right. do 2 eexists. split.
          * eapply gstep_run; eassumption.
          * cbv [val_sat]. eexists. split.
            -- cbn [forward_to graph_nodes]. rewrite get_map_values', map.get_put_same. simpl. reflexivity.
            -- simpl. rewrite H1p1p0. assumption.
    Qed.

    (*TODO replace stuff about initial_graph_state with hypotheses just about gs*)
    Lemma graph_eventually_of_node_eventually n P gs gt gns :
      star graph_step initial_gs gt gs ->
      map.get gs.(graph_nodes) n = Some gns ->
      eventually (will_step (node_step n) allowed) P (gns.(gns_node_state), gns.(gns_trace)) ->
      eventually graph_will_step
        (fun '(gs', _) =>
           val_sat gs'.(graph_nodes) n (fun gns' => P (gns'.(gns_node_state), gns'.(gns_trace))))
        (gs, gt).
    Proof.
      intros Hstar Hget Hev.
      remember (gns.(gns_node_state), gns.(gns_trace)) as nodeSt eqn:E.
      revert gs gt gns Hstar Hget E.
      induction Hev; intros gs gt gns Hstar Hget E; subst.
      { eauto. }
      eapply eventually_step_cps. apply will_step_reach. eapply will_step_impl.
      { eapply graph_will_step_of_node_will_step; eauto. }
      simpl. cbv [val_sat reachable]. intros. fwd. intros. fwd. eauto.
    Qed.

    Lemma consistently_incl_shrink_l l1 l1' l2 :
      submultiset l1' l1 ->
      consistently_incl equiv claim consistent l1 l2 ->
      consistently_incl equiv claim consistent l1' l2.
    Proof.
      intros Hsub [Hincl Hle]. split.
      - eapply (incl_mod_trans equiv);
          [ apply (incl_mod_of_submultiset equiv _ _ Hsub) | exact Hincl ].
      - intros s Hclaim' Hcons'. apply Hle.
        + eapply claim_mono; [ exact Hclaim' | apply (incl_mod_of_submultiset equiv _ _ Hsub) ].
        + eapply consistent_mono; [ exact Hcons' | exact Hsub ].
    Qed.

    Lemma consistently_incl_grow_r l1 l2 l2' :
      submultiset l2 l2' ->
      consistently_incl equiv claim consistent l1 l2 ->
      consistently_incl equiv claim consistent l1 l2'.
    Proof.
      intros Hsub [Hincl Hle]. split.
      - eapply (incl_mod_trans equiv);
          [ exact Hincl | apply (incl_mod_of_submultiset equiv _ _ Hsub) ].
      - intros s Hclaim' Hcons'.
        eapply consistent_mono; [ apply Hle; [ exact Hclaim' | exact Hcons' ] | exact Hsub ].
    Qed.

    Lemma consistently_incl_perm l1 l1' l2 l2' :
      Permutation l1 l1' -> Permutation l2 l2' ->
      consistently_incl equiv claim consistent l1 l2 ->
      consistently_incl equiv claim consistent l1' l2'.
    Proof.
      intros Hp1 Hp2 H.
      eapply consistently_incl_shrink_l; [ apply submultiset_perm, Permutation_sym; exact Hp1 | ].
      eapply consistently_incl_grow_r; [ apply submultiset_perm; exact Hp2 | exact H ].
    Qed.

    Lemma good_consistently_incl k v1 v2 :
      good_inputs_from k v2 ->
      incl_mod equiv v1 v2 ->
      consistently_incl equiv (fun s => claim_output s k) (fun s => consistent_output s k) v1 v2.
    Proof.
      intros (_ & Hgood2) Hincl. unfold consistently_incl. split; [ exact Hincl | ].
      cbv [consistent_le]. intros s Hcl _.
      apply Hgood2. eapply claim_output_mono; [ exact Hcl | exact Hincl ].
    Qed.


    Lemma fwd_partition_consistently_incl t2 gs1 gs2 n :
      star graph_step initial_gs t2 gs2 ->
      le_weak gs1 gs2 ->
      Forall2_map (fun sender => consistently_incl equiv (fun s => claim_output s (node_source sender))
                                                   (fun s => consistent_output s (node_source sender)))
        (fwd_partition n gs1.(graph_nodes)) (fwd_partition n gs2.(graph_nodes)).
    Proof.
      intros Hstar2 Hlew.
      eapply Forall2_map_impl_strong; [ apply (le_weak_fwd_partition _ _ n Hlew) | ].
      intros k v1 v2 _ Hv2 Hincl.
      apply good_consistently_incl; [ exact (fwd_partition_good _ _ n Hstar2 k v2 Hv2) | exact Hincl ].
    Qed.

    Lemma consistently_incl_of_le_weak t1 gs1 t2 gs2 :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      graph_inputs_allowed (flat_map inputs_of t1) ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      le_weak gs1 gs2 ->
      submultiset (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      Forall2_map (fun _ gns1 gns2 =>
                     consistently_incl equiv claim consistent
                       (flat_map inputs_of gns1.(gns_trace) ++ gns1.(gns_queue))
                       (flat_map inputs_of gns2.(gns_trace) ++ gns2.(gns_queue)))
        gs1.(graph_nodes) gs2.(graph_nodes).
    Proof.
      intros Hstar1 Hstar2 Hga1 Hga2 Hlew Hsub.
      eapply Forall2_map_impl_strong; [ apply (Forall2_map_map_values'_inv _ _ _ _ _ Hlew) | ].
      intros n gns1 gns2 Hg1 Hg2 _.
      pose proof (submultiset_matching_inps n _ _ Hsub) as Hmatch.
      eapply consistently_incl_perm;
        [ apply Permutation_sym; apply (inputs_are_outputs _ _ Hstar1 _ _ Hg1)
        | apply Permutation_sym; apply (inputs_are_outputs _ _ Hstar2 _ _ Hg2)
        | ].
      rewrite !fwd_total_eq.
      eapply consistently_incl_perm;
        [ eapply Permutation_trans; [ apply values_with_external | apply Permutation_app_comm ]
        | eapply Permutation_trans; [ apply values_with_external | apply Permutation_app_comm ]
        | ].
      apply consistently_incl_concat_values.
      - apply Forall_map_with_external;
          [ exact (Hga1 n)
          | intros k v Hk; exact (proj1 (fwd_partition_good _ _ n Hstar1 k v Hk)) ].
      - apply Forall_map_with_external;
          [ exact (Hga2 n)
          | intros k v Hk; exact (proj1 (fwd_partition_good _ _ n Hstar2 k v Hk)) ].
      - apply Forall2_map_with_external.
        + unfold consistently_incl. split.
          * apply (incl_mod_of_submultiset equiv). exact Hmatch.
          * cbv [consistent_le]. intros s _ Hcon.
            eapply consistent_output_mono; [ exact Hcon | exact Hmatch ].
        + eapply fwd_partition_consistently_incl; eassumption.
    Qed.


    Lemma gstep_pool_step g e g' :
      graph_step g e g' ->
      Forall2_map (fun _ ns1 ns2 =>
        submultiset (flat_map inputs_of ns1.(gns_trace)) (flat_map inputs_of ns2.(gns_trace)) /\
        submultiset (flat_map inputs_of ns1.(gns_trace) ++ ns1.(gns_queue))
                    (flat_map inputs_of ns2.(gns_trace) ++ ns2.(gns_queue))) g.(graph_nodes) g'.(graph_nodes).
    Proof.
      intros Hstep. invert Hstep; try invert_receive; cbn [forward_to graph_nodes graph_output_queue].
      - apply Forall2_map_map_values'_r. apply Forall2_map_dup. intros k v Hv.
        cbn [enqueue gns_trace gns_queue]. split.
        + apply submultiset_refl.
        + eauto with submultiset perm.
      - apply Forall2_map_map_values'_r. eapply Forall2_map_put_r; [ | exact H | ].
        + apply Forall2_map_dup. intros k v Hv Hne. cbn [enqueue gns_trace gns_queue]. split.
          * apply submultiset_refl.
          * eauto with submultiset perm.
        + cbn [enqueue gns_trace gns_queue].
          cbn [flat_map inputs_of app].
          split.
          * apply submultiset_refl.
          * eauto with submultiset perm.
      - eapply Forall2_map_put_r; [ | exact H | ].
        + apply Forall2_map_dup. intros k v Hv Hne. split; apply submultiset_refl.
        + cbn [gns_trace gns_queue].
          cbn [flat_map inputs_of app].
          split.
          * apply submultiset_cons.
          * rewrite H2. eauto with submultiset perm.
      - apply Forall2_map_refl. intros. split; apply submultiset_refl.
    Qed.

    Lemma pool_submultiset g1 T g2 :
      star graph_step g1 T g2 ->
      Forall2_map (fun _ ns1 ns2 =>
        submultiset (flat_map inputs_of ns1.(gns_trace)) (flat_map inputs_of ns2.(gns_trace)) /\
        submultiset (flat_map inputs_of ns1.(gns_trace) ++ ns1.(gns_queue))
                    (flat_map inputs_of ns2.(gns_trace) ++ ns2.(gns_queue))) g1.(graph_nodes) g2.(graph_nodes).
    Proof.
      induction 1 as [ | t0 s' e s'' Hstar IH Hstep ].
      - apply Forall2_map_refl. intros. split; apply submultiset_refl.
      - eapply Forall2_map_trans; [ | exact IH | apply (gstep_pool_step _ _ _ Hstep) ].
        intros k a b c (Ht1 & Hp1) (Ht2 & Hp2). split; eapply submultiset_trans; eassumption.
    Qed.

    Lemma eventually_deliver n :
      forall N owed gc tc nc,
        length owed <= N ->
        map.get gc.(graph_nodes) n = Some nc ->
        submultiset owed (gns_queue nc) ->
        eventually graph_will_step
          (fun '(gc', _) => exists nc', map.get gc'.(graph_nodes) n = Some nc' /\
             submultiset (flat_map inputs_of (gns_trace nc) ++ owed) (flat_map inputs_of (gns_trace nc')))
          (gc, tc).
    Proof.
      induction N as [| N IHN]; intros owed gc tc nc HN Hget Hsub;
        (destruct owed as [| a owed'];
         [ apply eventually_done; exists nc; split;
           [ exact Hget | rewrite app_nil_r; apply submultiset_refl ] | ]).
      - simpl in HN. inversion HN.
      - assert (Hlen' : length owed' <= N) by (simpl in HN; apply le_S_n; exact HN).
        assert (Ha_q : submultiset [a] (gns_queue nc)).
        { eapply submultiset_trans; [ | exact Hsub ]. exists owed'. reflexivity. }
        eapply eventually_step_cps. exists (receive n a).
        intros gs_d td Hstar_d _.
        pose proof (pool_submultiset gc td gs_d Hstar_d) as Hls.
        destruct (Forall2_map_get_l _ _ _ _ _ Hls Hget) as (nd & Hget_d & Htr & Htot).
        assert (Htot_owed : submultiset (flat_map inputs_of (gns_trace nc) ++ a :: owed')
                              (flat_map inputs_of (gns_trace nd) ++ gns_queue nd))
          by (eapply submultiset_trans; [ apply submultiset_app_head; exact Hsub | exact Htot ]).
        destruct (classic (In a (gns_queue nd))) as [Hin_d | Hnin_d].
        + apply in_split in Hin_d. destruct Hin_d as (ms1 & ms2 & Hq).
          destruct (nodes_input_total n (gns_node_state nd) a) as (nd' & Hns).
          right. eexists _, []. split.
          { eapply gstep_receive; [ exact Hget_d | eapply receive_step_intro; [ exact Hns | exact Hq ] ]. }
          set (ndr := {| gns_node_state := nd'; gns_trace := I_event a :: gns_trace nd;
                         gns_queue := ms1 ++ ms2 |}).
          assert (Hget_r : map.get (map.put gs_d.(graph_nodes) n ndr) n = Some ndr)
            by (apply map.get_put_same).
          assert (Hitr : flat_map inputs_of (gns_trace ndr) = a :: flat_map inputs_of (gns_trace nd))
            by reflexivity.
          assert (Ha_del : submultiset (flat_map inputs_of (gns_trace nc) ++ [a]) (flat_map inputs_of (gns_trace ndr))).
          { rewrite Hitr. eapply submultiset_perm_l; [ apply Permutation_cons_append | ].
            apply submultiset_cons_mono. exact Htr. }
          assert (Htot_r : submultiset (flat_map inputs_of (gns_trace nc) ++ a :: owed')
                             (flat_map inputs_of (gns_trace ndr) ++ gns_queue ndr)).
          { eapply submultiset_perm_r; [ | exact Htot_owed ].
            rewrite Hitr. subst ndr. cbn [gns_queue]. rewrite Hq.
            rewrite !app_assoc. symmetry. apply Permutation_middle. }
          destruct (submultiset_absorb (flat_map inputs_of (gns_trace nc)) owed'
                      (flat_map inputs_of (gns_trace ndr)) (gns_queue ndr) a Htot_r Ha_del)
            as (owed'' & HQ'' & Habs & Hlen'').
          eapply eventually_weaken.
          { eapply IHN. 2,3: eassumption. lia. }
          simpl. intros. fwd. eauto using submultiset_trans.
        + left.
          assert (Ha_del : submultiset (flat_map inputs_of (gns_trace nc) ++ [a]) (flat_map inputs_of (gns_trace nd))).
          { eapply submultiset_cons_of_not_in; [ exact Htr | | exact Hnin_d ].
            eapply submultiset_trans; [ apply submultiset_app_head; exact Ha_q | exact Htot ]. }
          destruct (submultiset_absorb (flat_map inputs_of (gns_trace nc)) owed'
                      (flat_map inputs_of (gns_trace nd)) (gns_queue nd) a Htot_owed Ha_del)
            as (owed'' & HQ'' & Habs & Hlen'').
          eapply eventually_weaken.
          { eapply IHN. 2,3: eassumption. lia. }
          simpl. intros. fwd. eauto using submultiset_trans.
    Qed.

    Lemma eventually_received t2 gs2 :
      eventually graph_will_step
        (fun '(gs2', _) =>
           Forall2_map (fun _ ns2 ns2' =>
                          submultiset (flat_map inputs_of ns2.(gns_trace) ++ ns2.(gns_queue))
                            (flat_map inputs_of ns2'.(gns_trace))) gs2.(graph_nodes) gs2'.(graph_nodes))
        (gs2, t2).
    Proof.
      apply eventually_will_step_reach.
      eapply eventually_weaken.
      { eapply eventually_will_step_Forall with
          (Ps := map (fun '(k, v) =>
                    (fun '(gc', _) => exists nc', map.get gc'.(graph_nodes) k = Some nc' /\
                       submultiset (flat_map inputs_of (gns_trace v) ++ gns_queue v)
                                   (flat_map inputs_of (gns_trace nc'))))
                    (map.tuples gs2.(graph_nodes))).
        - apply List.Forall_map. apply Forall_forall. intros [k v] _.
          cbv [ev_stable]. intros s s' e t (nc' & Hgk & Hsm) Hstep.
          pose proof (gstep_pool_step _ _ _ Hstep) as Hls.
          destruct (Forall2_map_get_l _ _ _ _ _ Hls Hgk) as (nc'' & Hgk' & Htr & _).
          eauto using submultiset_trans.
        - apply List.Forall_map. apply Forall_forall. intros [k v] Hin.
          apply map.tuples_spec in Hin.
          eapply (eventually_deliver k (length (gns_queue v)) (gns_queue v) gs2 t2 v);
            [ apply le_n | exact Hin | apply submultiset_refl ]. }
      intros [gs2' t2'] Hall Hreach.
      destruct Hreach as (tr & Hstar_gg & _ & _).
      pose proof (pool_submultiset _ _ _ Hstar_gg) as Hls.
      eapply Forall2_map_impl_strong; [ exact Hls | ].
      intros k v v' Hgk Hgk' _. rewrite Forall_forall in Hall.
      destruct (Hall _ ltac:(apply in_map_iff; exists (k, v);
                  split; [ reflexivity | apply map.tuples_spec; exact Hgk ]))
        as (nc' & Hgk'' & Hsm).
      rewrite Hgk' in Hgk''. injection Hgk'' as <-. exact Hsm.
    Qed.

    Lemma le_weak_to_le gs1 t1 gs2 t2 :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      submultiset (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      le_weak gs1 gs2 ->
      eventually graph_will_step (fun '(gs2', _) => le gs1 gs2') (gs2, t2).
    Proof.
      intros Hstar1 Hstar2 Hsub Hga2 Hlew.
      assert (Hga1 : graph_inputs_allowed (flat_map inputs_of t1))
        by (eapply graph_inputs_allowed_submultiset; eassumption).
      apply eventually_will_step_reach.
      eapply eventually_weaken.
      { exact (eventually_received _ _). }
      intros [gs2' t2'] Hrecv _.
      cbv [le].
      eapply Forall2_map_compose; [| |eassumption].
      2: { eapply consistently_incl_of_le_weak; eassumption. }
      intros k a b c Hci Hrec.
      eapply consistently_incl_shrink_l; [ apply submultiset_app_r | ].
      eapply consistently_incl_grow_r; [ exact Hrec | exact Hci ].
    Qed.

    Lemma node_will_match' gs1 t1 lbl outs gs1' gs2 t2 :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      graph_inputs_allowed (flat_map inputs_of t1) ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      graph_step gs1 (O_event lbl outs) gs1' ->
      le gs1 gs2 ->
      le_weak gs1 gs2 ->
      eventually graph_will_step
        (fun '(gs2', _) => le_weak gs1' gs2') (gs2, t2).
    Proof.
      intros H1 H2 H3 H4 Hncgi Hstep Hle Hlew.
      epose proof Forall2_map_get_l as Hle'. specialize Hle' with (1 := Hle).
      epose proof (graph_step_to_node_step_from_beginning gs1 t1) as Hns1'.
      epose proof (graph_step_to_node_step_from_beginning gs2 t2) as Hns2'.
      especialize Hns1'; eauto. especialize Hns2'; eauto.
      invert Hstep; try invert_receive.
      - especialize Hle'; eauto. fwd. map_func.
        eapply Forall2_map_get_r in Hns1'; eauto. fwd.
        eapply Forall2_map_get_l in Hns2'; eauto. fwd.
        map_func. simpl in *.
        pose proof nodes_good as H'. cbv [Forall_map] in H'. especialize H'; eauto.
        cbv [node_good] in H'. fwd.
        pose proof (everything_allowed _ gs1 ltac:(eauto) ltac:(eauto)) as Hall1.
        pose proof (everything_allowed _ gs2 ltac:(eauto) ltac:(eauto)) as Hall2.
        assert (allowed (flat_map inputs_of (gns_trace ns) ++ gns_queue ns)) by eauto.
        assert (allowed (flat_map inputs_of (gns_trace v0) ++ gns_queue v0)) by eauto.
        assert (Hnc_n : noncontradictory (flat_map inputs_of (gns_trace ns)) (flat_map inputs_of (gns_trace v0))).
        { eapply (node_inputs_noncontradictory n t1 gs1 t2 gs2 ns v0); try eassumption.
          eapply everything_noncontradictory; eassumption. }
        eassert (Hmo: Forall (might_output_equiv _ _ (gns_node_state v0) (gns_trace v0)) outs0).
        { apply Forall_forall. intros m Hm.
          eapply (H'p1 (gns_trace ns)). all: eauto. eauto 10. }
        eapply will_output_all in Hmo; eauto.
        eapply graph_eventually_of_node_eventually in Hmo; eauto.
        apply eventually_will_step_reach.
        eapply eventually_weaken; [eassumption|].
        cbv [val_sat reachable]. intros [r l] Hval Hreach. fwd.
        pose proof (le_weak_trans _ _ _ Hlew (star_gstep_le_weak _ _ _ Hreachp0)) as Hlwr.
        cbv [le_weak] in Hlwr |- *. cbn [forward_to graph_nodes].
        rewrite outputs_partition_map_values' by reflexivity.
        rewrite outputs_partition_put. simpl.
        eapply Forall2_map_put_l.
        + eapply Forall2_map_impl; eauto.
        + rewrite outputs_partition_get, Hvalp0. simpl. reflexivity.
        + apply incl_mod_app.
          -- intros x Hx. rewrite Forall_forall in Hvalp1.
             especialize Hvalp1; eauto. fwd. eauto.
          -- specialize (Hlwr n). rewrite !outputs_partition_get, H6, Hvalp0 in Hlwr.
             cbn [option_map] in Hlwr. exact Hlwr.
      - especialize Hle'; eauto. fwd.
        apply eventually_done. cbv [le_weak]. cbn [forward_to graph_nodes].
        erewrite outputs_partition_put_output_eq by (eassumption || reflexivity).
        exact Hlew.
      - apply eventually_done. exact Hlew.
    Qed.

    Lemma node_will_match gs1 t1 lbl outs gs1' gs2 t2 :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      submultiset (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      graph_inputs_allowed (flat_map inputs_of t1) ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      graph_step gs1 (O_event lbl outs) gs1' ->
      le gs1 gs2 ->
      le_weak gs1 gs2 ->
      eventually graph_will_step
        (fun '(gs2', _) => le gs1' gs2' /\ le_weak gs1' gs2') (gs2, t2).
    Proof.
      intros Hstar1 Hstar2 Hsub Hga1 Hga2 Hncgi Hstep Hle Hlew.
      assert (Hstar1' : star graph_step initial_gs (O_event lbl outs :: t1) gs1') by eauto.
      pose proof (node_will_match' _ _ _ _ _ _ _ Hstar1 Hstar2 Hga1 Hga2 Hncgi Hstep Hle Hlew) as Hev.
      apply eventually_will_step_annotate in Hev.
      eapply eventually_trans; [ exact Hev | ].
      intros [gs2' t2'] (Hreach' & Hlw).
      destruct Hreach' as (tr & Hstar_gg & -> & Hga_imp).
      assert (Hstar2' : star graph_step initial_gs (tr ++ t2) gs2') by eauto using star_app.
      specialize (Hga_imp Hga2).
      assert (Hsub' : submultiset (flat_map inputs_of (O_event lbl outs :: t1)) (flat_map inputs_of (tr ++ t2))).
      { rewrite flat_map_app. simpl.
        eapply submultiset_trans;
          [ exact Hsub | exists (flat_map inputs_of tr); apply Permutation_app_comm ]. }
      pose proof (le_weak_to_le _ _ _ _ Hstar1' Hstar2' Hsub' Hga_imp Hlw) as Hle2.
      eapply eventually_weaken.
      { eapply eventually_carry_stable_gen with (P := (fun '(s, _) => le_weak gs1' s));
          [ | exact Hlw | exact Hle2 ].
        intros s s' e t Hlws Hst.
        eapply le_weak_trans;
          [ exact Hlws | exact (star_gstep_le_weak _ _ _ (star_one _ _ _ _ Hst)) ]. }
      intros [s t] (Hlw_s & Hle_s). split; [ exact Hle_s | exact Hlw_s ].
    Qed.

    Lemma le_node_output t1 gs1 gs2 t2 n o :
      star graph_step initial_gs t1 gs1 ->
      star graph_step initial_gs t2 gs2 ->
      graph_inputs_allowed (flat_map inputs_of t1) ->
      graph_inputs_allowed (flat_map inputs_of t2) ->
      noncontradictory_graph_inputs (flat_map inputs_of t1) (flat_map inputs_of t2) ->
      le gs1 gs2 ->
      node_has_output gs1 n o ->
      eventually graph_will_step
        (fun '(gs2', _) => exists o', node_has_output gs2' n o' /\ equiv o' o) (gs2, t2).
    Proof.
      intros Hstar1 Hstar2 Hga1 Hga2 Hncgi Hle (ns1 & Hget1 & Hout1).
      destruct (Forall2_map_get_l _ _ _ _ _ Hle Hget1) as (ns2 & Hget2 & Hincl).
      destruct (Forall2_map_get_r _ _ _ _ _ (node_run_allowed t1 gs1 Hstar1 Hga1) Hget1)
        as (ns0 & Hget0 & Hrun1 & Hall1).
      destruct (Forall2_map_get_r _ _ _ _ _ (node_run_allowed t2 gs2 Hstar2 Hga2) Hget2)
        as (ns0' & Hget0' & Hrun2 & Hall2).
      assert (ns0' = ns0) by (rewrite Hget0 in Hget0'; congruence). subst ns0'.
      pose proof (nodes_good n ns0 Hget0) as (_ & Hmono & Hmiw).
      assert (Hmiw' : might_implies_will_equiv' (node_step n) equiv claim consistent allowed
                        (gns_node_state ns0)).
      { apply miw'_iff_miw_and_monotone'; try assumption;
          try (split; [ exact Hmiw | exact Hmono ]). }
      assert (Hnc_n : noncontradictory (flat_map inputs_of (gns_trace ns1)) (flat_map inputs_of (gns_trace ns2))).
      { eapply (node_inputs_noncontradictory n t1 gs1 t2 gs2 ns1 ns2); try eassumption.
        eapply everything_noncontradictory; eassumption. }
      pose proof (Hmiw' _ _ _ Hrun1 Hall1 Hout1 _ _ Hnc_n Hincl Hrun2 Hall2) as Hwoe.
      eapply eventually_weaken.
      { eapply (graph_eventually_of_node_eventually n _ gs2 t2 ns2);
          [ exact Hstar2 | exact Hget2 | exact Hwoe ]. }
      intros [gs2' t2'] (gns' & Hgetf & o' & Hequiv & Hino').
      exists o'. split; [ exists gns'; split; [ exact Hgetf | exact Hino' ] | exact Hequiv ].
    Qed.

    Lemma in_output_total gs o :
      In o (output_total gs) ->
      exists n, node_has_output gs n o /\ forward (node_source n) output_destn o = true.
    Proof.
      unfold output_total, output_map. intros Hin.
      apply In_concat_values in Hin. destruct Hin as (k & vs & Hget & Hin).
      rewrite get_map_values', outputs_partition_get in Hget.
      rewrite option_map_option_map in Hget. apply option_map_Some in Hget. fwd.
      apply filter_In in Hin. fwd. exists k. split; [ cbv [node_has_output]; eauto | assumption ].
    Qed.

    Lemma output_total_in gs n m :
      node_has_output gs n m -> forward (node_source n) output_destn m = true -> In m (output_total gs).
    Proof.
      intros (v & Hget & Hinout) Hvis.
      unfold output_total, output_map. apply In_concat_values.
      do 2 eexists.
      split.
      - rewrite get_map_values', outputs_partition_get, Hget. reflexivity.
      - apply filter_In. eauto.
    Qed.

    Lemma drive_to_dominate t0 gs0 t' gs_f :
      star graph_step initial_gs t0 gs0 ->
      graph_inputs_allowed (flat_map inputs_of t0) ->
      star graph_step gs0 t' gs_f ->
      flat_map inputs_of t' = [] ->
      eventually graph_will_step
        (fun '(gs2, _) => le gs_f gs2 /\ le_weak gs_f gs2) (gs0, t0).
    Proof.
      intros Hstar0 Hga0 Hrun Hinp. revert Hinp.
      induction Hrun as [ | T0 gmid e gsf Hrun' IH Hstep ]; intros Hinp.
      - apply eventually_done. split; [ apply le_refl | apply le_weak_refl ].
      - destruct e as [m | lbl outs].
        { discriminate Hinp. }
        simpl in Hinp.
        specialize (IH Hinp).
        apply eventually_will_step_annotate in IH.
        eapply eventually_trans; [ exact IH | ].
        intros [gs2 t2] (Hreach & Hle_mid & Hlw_mid).
        destruct Hreach as (tr & Hstar_gg & -> & Hga_imp).
        specialize (Hga_imp Hga0).
        assert (Hstar2 : star graph_step initial_gs (tr ++ t0) gs2) by eauto using star_app.
        assert (Hstarmid : star graph_step initial_gs (T0 ++ t0) gmid) by eauto using star_app.
        assert (Hgamid : graph_inputs_allowed (flat_map inputs_of (T0 ++ t0))).
        { rewrite flat_map_app, Hinp. cbn [app]. exact Hga0. }
        assert (Hsub : submultiset (flat_map inputs_of (T0 ++ t0)) (flat_map inputs_of (tr ++ t0))).
        { rewrite !flat_map_app, Hinp. cbn [app].
          eexists. apply Permutation_app_comm. }
        assert (Hncgi : noncontradictory_graph_inputs (flat_map inputs_of (T0 ++ t0)) (flat_map inputs_of (tr ++ t0)))
          by (apply noncontradictory_graph_inputs_of_submultiset; assumption).
        eauto using node_will_match.
    Qed.

    Lemma gstep_output_queue_step g e g' :
      graph_step g e g' ->
      submultiset g.(graph_output_queue) (outputs_of e ++ g'.(graph_output_queue)).
    Proof.
      intros Hstep. invert Hstep; try invert_receive; cbn [forward_to graph_output_queue outputs_of app].
      - apply submultiset_app_l.
      - apply submultiset_app_l.
      - apply submultiset_refl.
      - match goal with H : graph_output_queue _ = _ |- _ => rewrite H end.
        apply submultiset_perm. symmetry. apply Permutation_middle.
    Qed.

    Lemma output_queue_submultiset g t g' :
      star graph_step g t g' ->
      submultiset g.(graph_output_queue) (flat_map outputs_of t ++ g'.(graph_output_queue)).
    Proof.
      induction 1 as [ | t0 smid e sfin Hstar IH Hstep ].
      - apply submultiset_refl.
      - eapply submultiset_trans; [ exact IH | ].
        cbn [flat_map]. rewrite <- app_assoc.
        eapply submultiset_perm_r; [ apply Permutation_app_swap_app | ].
        apply submultiset_app_head. apply gstep_output_queue_step. exact Hstep.
    Qed.

    Lemma eventually_emit gc tc m :
      In m gc.(graph_output_queue) ->
      eventually graph_will_step (fun '(_, t) => In m (flat_map outputs_of t)) (gc, tc).
    Proof.
      intros Hm. apply eventually_step_cps. cbv [graph_will_step will_step]. exists (emit m).
      intros s' t' Hstar _.
      pose proof (submultiset_incl _ _ (output_queue_submultiset _ _ _ Hstar) m Hm) as Hin.
      apply in_app_or in Hin. destruct Hin as [Hout | Hq].
      - left. apply eventually_done. rewrite flat_map_app, in_app_iff. left. exact Hout.
      - apply in_split in Hq. destruct Hq as (q1 & q2 & Hq).
        right. do 2 eexists. split.
        + apply gstep_output. exact Hq.
        + apply eventually_done. cbn [flat_map outputs_of]. now left.
    Qed.

    Lemma graph_might_implies_will :
      might_implies_will_equiv graph_step equiv graph_inputs_allowed initial_gs.
    Proof.
      intros t gs o Hstar Hga (t' & gs_f & Hrun & Hinp & Hino).
      assert (Hstarf : star graph_step initial_gs (t' ++ t) gs_f) by eauto using star_app.
      assert (Hgaf : graph_inputs_allowed (flat_map inputs_of (t' ++ t))).
      { rewrite flat_map_app, Hinp. cbn [app]. exact Hga. }
      unfold will_output_equiv.
      (* a drained output is either produced by a node or forwarded straight from an input *)
      assert (Hcases : In o (output_total gs_f) \/
                       In o (filter (forward input_source output_destn) (flat_map inputs_of (t' ++ t)))).
      { apply in_app_or. eapply Permutation_in;
          [ apply (outputs_are_node_outputs (t' ++ t) gs_f Hstarf) | ].
        rewrite in_app_iff. left. exact Hino. }
      destruct Hcases as [Hnode | Hinput].
      - apply in_output_total in Hnode. destruct Hnode as (n & Hnho & Hvis).
        pose proof (drive_to_dominate t gs t' gs_f Hstar Hga Hrun Hinp) as Hdrive.
        apply eventually_will_step_annotate in Hdrive.
        eapply eventually_trans; [ exact Hdrive | ].
        intros [gs2 t2] (Hreach & Hle2 & _).
        destruct Hreach as (tr & Hstar_gg & -> & Hga_imp). specialize (Hga_imp Hga).
        assert (Hstar2 : star graph_step initial_gs (tr ++ t) gs2) by eauto using star_app.
        assert (Hncgi : noncontradictory_graph_inputs (flat_map inputs_of (t' ++ t)) (flat_map inputs_of (tr ++ t))).
        { apply noncontradictory_graph_inputs_of_submultiset; [ | exact Hga_imp ].
          rewrite !flat_map_app, Hinp. cbn [app]. apply submultiset_app_l. }
        pose proof (le_node_output (t' ++ t) gs_f gs2 (tr ++ t) n o
                      Hstarf Hstar2 Hgaf Hga_imp Hncgi Hle2 Hnho) as Hemit.
        apply eventually_will_step_annotate in Hemit.
        eapply eventually_trans; [ exact Hemit | ].
        intros [gs2' t2'] (Hreach' & m' & Hnho' & Heqm).
        destruct Hreach' as (tr' & Hstar_gg' & -> & _).
        assert (Hstar2' : star graph_step initial_gs (tr' ++ tr ++ t) gs2') by eauto using star_app.
        assert (Hvis' : forward (node_source n) output_destn m' = true).
        { rewrite (forward_equiv (node_source n) output_destn m' o Heqm). exact Hvis. }
        assert (Hmq : In m' (flat_map outputs_of (tr' ++ tr ++ t)) \/ In m' gs2'.(graph_output_queue)).
        { apply in_app_or. eapply Permutation_in;
            [ apply Permutation_sym; apply (outputs_are_node_outputs _ gs2' Hstar2') | ].
          rewrite in_app_iff. left. apply (output_total_in _ n); assumption. }
        eapply eventually_weaken with (P := fun '(_, ot) => In m' (flat_map outputs_of ot)).
        + destruct Hmq as [Hout | Hq].
          * apply eventually_done. exact Hout.
          * apply eventually_emit. exact Hq.
        + intros [s ot] Hm. exists m'. split; [ exact Heqm | exact Hm ].
      - rewrite flat_map_app, Hinp in Hinput.
        assert (Hoq : In o (flat_map outputs_of t) \/ In o gs.(graph_output_queue)).
        { apply in_app_or. eapply Permutation_in;
            [ apply Permutation_sym; apply (outputs_are_node_outputs t gs Hstar) | ].
          rewrite in_app_iff. right. exact Hinput. }
        eapply eventually_weaken with (P := fun '(_, ot) => In o (flat_map outputs_of ot)).
        + destruct Hoq as [Hout | Hq].
          * apply eventually_done. exact Hout.
          * apply eventually_emit. exact Hq.
        + intros [s ot] Hm. exists o. split; [ reflexivity | exact Hm ].
    Qed.
  End graph.
  Arguments graph_node_state : clear implicits.
  Arguments graph_state node_state {m1}.

  Section graphs.
    Context {node_state1 : Type}.
    Context {m1 : map.map node_id (graph_node_state node_state1)}.
    Context {m1_ok : map.ok m1}.
    Context (initial_gs1 : graph_state node_state1).
    Context (node_step1 : node_id -> node_state1 -> IO_event -> node_state1 -> Prop).

    Context {node_state2 : Type}.
    Context {m2 : map.map node_id (graph_node_state node_state2)}.
    Context {m2_ok : map.ok m2}.
    Context (initial_gs2 : graph_state node_state2).
    Context (node_step2 : node_id -> node_state2 -> IO_event -> node_state2 -> Prop).

    Lemma graphs_corresp_sound' :
      Forall2_map
        (fun n gns1 gns2 =>
           steps_corresp_sound allowed
             (node_step1 n) gns1.(gns_node_state)
             (node_step2 n) gns2.(gns_node_state))
        initial_gs1.(graph_nodes) initial_gs2.(graph_nodes) ->
      steps_corresp_sound' graph_inputs_allowed equiv
        (graph_step node_step1) initial_gs1
        (graph_step node_step2) initial_gs2.
    Proof.
    Admitted.
  End graphs.

End __.

Arguments graph_node_state : clear implicits.
Arguments graph_label : clear implicits.
Arguments graph_state message label node_state {m1}.

Ltac invert_receive :=
  match goal with H : receive_step _ _ _ _ _ |- _ => invert H end.
